PGFile: GrammarRules
      | Imports GrammarRules
      | GrammarRules 'terminals' TerminalRules
      | Imports GrammarRules 'terminals' TerminalRules
      | 'terminals' TerminalRules;

Imports: Imports ImportStm | ImportStm;
ImportStm: 'import' StrConst
         | 'import' StrConst 'as' Name;

GrammarRules: GrammarRules GrammarRule
            | GrammarRule;
GrammarRule: ActionOpt Name ':' GrammarRuleRHS ';'
           | ActionOpt Name '{' ProdMetaDatas '}' ':' GrammarRuleRHS ';';
ActionOpt: Action | EMPTY;
GrammarRuleRHS {left, 5}: GrammarRuleRHS '|' Production
                        | Production;
Production: Assignments
          | Assignments '{' ProdMetaDatas '}';

TerminalRules: TerminalRules TerminalRule
             | TerminalRule;
TerminalRule {left, 15}: ActionOpt Name ':' Recognizer ';'
                       | ActionOpt Name ':' ';'
                       | ActionOpt Name ':' Recognizer '{' TermMetaDatas '}' ';'
                       | ActionOpt Name ':' '{' TermMetaDatas '}' ';';

ProdMetaData: 'left' | 'reduce' | 'right' | 'shift' | 'dynamic' | 'nops' | 'nopse'
            | IntConst
            | UserMetaData;
ProdMetaDatas {left}: ProdMetaDatas ',' ProdMetaData | ProdMetaData;

TermMetaData: 'prefer' | 'finish' | 'nofinish' | 'dynamic'
            | IntConst
            | UserMetaData;
TermMetaDatas: TermMetaDatas ',' TermMetaData | TermMetaData;

UserMetaData: Name ':' Const;
Const: IntConst | FloatConst | BoolConst | StrConst;

Assignment: PlainAssignment
          | BoolAssignment
          | GrammarSymbolRef;
Assignments: Assignments Assignment | Assignment;
PlainAssignment: Name '=' GrammarSymbolRef;
BoolAssignment: Name '?=' GrammarSymbolRef;

ProductionGroup: '(' GrammarRuleRHS ')';

GrammarSymbolRef: GrammarSymbol RepetitionOperatorOpt
                | ProductionGroup RepetitionOperatorOpt;
RepetitionOperator: RepetitionOperatorOp RepetitionModifiersExpOpt;
RepetitionOperatorOpt: RepetitionOperator | EMPTY;
RepetitionOperatorOp: '*' | '*!' | '+' | '+!' | '?' | '?!';
RepetitionModifiersExp: '[' RepetitionModifiers ']';
RepetitionModifiersExpOpt: RepetitionModifiersExp | EMPTY;
RepetitionModifiers: RepetitionModifiers ',' RepetitionModifier | RepetitionModifier;
RepetitionModifier: Name;

GrammarSymbol: Name | StrConst;
Recognizer: StrConst | RegexTerm;

Layout: LayoutItem | Layout LayoutItem | EMPTY;
LayoutItem: WS | Comment;
Comment: '/*' Corncs '*/' | CommentLine;
Corncs: Cornc | Corncs Cornc | EMPTY;
Cornc: Comment | NotComment | WS;

terminals

Terminals: 'terminals';
Import: 'import';
As: 'as';
Colon: ':';
SemiColon: ';';
Comma: ',';
OBrace: '{';
CBrace: '}';
OBracket: '(';
CBracket: ')';
OSBracket: '[';
CSBracket: ']';
Choice: '|';
ZeroOrMore: '*';
ZeroOrMoreGreedy: '*!';
OneOrMore: '+';
OneOrMoreGreedy: '+!';
Optional: '?';
OptionalGreedy: '?!';
Equals: '=';
QEquals: '?=';
Left: 'left';
Right: 'right';
Reduce: 'reduce';
Shift: 'shift';
Dynamic: 'dynamic';
NOPS: 'nops';
NOPSE: 'nopse';
Prefer: 'prefer';
Finish: 'finish';
NoFinish: 'nofinish';
OComment: '/*';
CComment: '*/';
Name: /[a-zA-Z_][a-zA-Z0-9_\.]*/;
RegexTerm: /'\/(\\.|[^\/\\])*\/'/;
IntConst: /\d+/;
FloatConst: /[+-]?(\d+\.\d*|\.\d+)([eE][+-]?\d+)?(?<=[\w\.])(?![\w\.])/;
BoolConst: /true|false/;
StrConst: /(?s)('[^'\\]*(?:\\.[^'\\]*)*')|("[^"\\]*(?:\\.[^"\\]*)*")/;
Action: /@[a-zA-Z0-9_]+/;
WS: /\s+/;
CommentLine: /\/\/.*/;
NotComment: /((\*[^\/])|[^\s*\/]|\/[^\*])+/;
