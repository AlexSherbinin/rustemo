File: GrammarRule+
    | ImportStm+ GrammarRule+
    | GrammarRule+ 'terminals' TerminalRule+
    | ImportStm+ GrammarRule+ 'terminals' TerminalRule+
    | 'terminals' TerminalRule+;

ImportStm: 'import' path=StrConst
         | 'import' path=StrConst 'as' Name;

GrammarRule: Annotation? Name ':' rhs=GrammarRuleRHS ';'
           | Annotation? Name '{' meta=ProdMetaDatas '}' ':' rhs=GrammarRuleRHS ';';
GrammarRuleRHS: rhs=GrammarRuleRHS '|' Production
              | Production;
Production: Assignment+
          | Assignment+ '{' meta=ProdMetaDatas '}';

TerminalRule: Annotation? Name ':' Recognizer ';'
            | Annotation? Name ':' ';'
            | Annotation? Name ':' Recognizer '{' meta=TermMetaDatas '}' ';'
            | Annotation? Name ':' '{' meta=TermMetaDatas '}' ';';

ProdMetaData: 'left' {Left} | 'reduce' {Reduce} | 'right' {Right} | 'shift' {Shift}
            | 'dynamic' {Dynamic} | 'nops' {NOPS} | 'nopse' {NOPSE}
            | IntConst {Priority}
            | UserMetaData
            | ProdKind;
ProdMetaDatas: metas=ProdMetaDatas ',' meta=ProdMetaData | meta=ProdMetaData;

TermMetaData: 'prefer' {Prefer} | 'finish' {Finish} | 'nofinish' {NoFinish}
            | 'dynamic' {Dynamic}
            | IntConst {Priority}
            | UserMetaData;
TermMetaDatas: metas=TermMetaDatas ',' meta=TermMetaData | meta=TermMetaData;

UserMetaData: Name ':' value=ConstVal;
ProdKind: Name;
ConstVal: IntConst | FloatConst | BoolConst | StrConst;

Assignment: PlainAssignment
          | BoolAssignment
          | GrammarSymbolRef;
PlainAssignment: Name '=' gsymref=GrammarSymbolRef;
BoolAssignment: Name '?=' gsymref=GrammarSymbolRef;

ProductionGroup: '(' prod_rule_rhs=GrammarRuleRHS ')';

GrammarSymbolRef: gsymbol=GrammarSymbol RepetitionOperator?
                | ProductionGroup RepetitionOperator?;
RepetitionOperator: RepetitionOperatorOp RepetitionModifiers?;
RepetitionOperatorOp: '*' {ZeroOrMore}
                    | '*!' {ZeroOrMoreGreedy}
                    | '+' {OneOrMore}
                    | '+!' {OneOrMoreGreedy}
                    | '?' {Optional}
                    | '?!' {OptionalGreedy};
RepetitionModifiers: '[' RepetitionModifier+[Comma] ']';
RepetitionModifier: Name;

GrammarSymbol: Name | StrConst;
Recognizer: StrConst | RegexTerm;

// ANCHOR: layout
Layout: LayoutItem*;
LayoutItem: WS | Comment;
Comment: '/*' Corncs '*/' | CommentLine;
Corncs: Cornc*;
Cornc: Comment | NotComment | WS;
// ANCHOR_END: layout

terminals

Terminals: 'terminals';
Import: 'import';
As: 'as';
Colon: ':';
SemiColon: ';';
Comma: ',';
OBrace: '{';
CBrace: '}';
OBracket: '(';
CBracket: ')';
OSBracket: '[';
CSBracket: ']';
Choice: '|';
ZeroOrMore: '*';
ZeroOrMoreGreedy: '*!';
OneOrMore: '+';
OneOrMoreGreedy: '+!';
Optional: '?';
OptionalGreedy: '?!';
Equals: '=';
QEquals: '?=';
Left: 'left';
Right: 'right';
Reduce: 'reduce';
Shift: 'shift';
Dynamic: 'dynamic';
NOPS: 'nops';
NOPSE: 'nopse';
Prefer: 'prefer';
Finish: 'finish';
NoFinish: 'nofinish';
OComment: '/*';
CComment: '*/';
Name: /[a-zA-Z_][a-zA-Z0-9_\.]*/;
RegexTerm: /\/(\\.|[^\/\\])*\//;
IntConst: /\d+/;
FloatConst: /[+-]?[0-9]+[.][0-9]*([e][+-]?[0-9]+)?/;
BoolConst: /true|false/;
StrConst: /(?s)(^'[^'\\]*(?:\\.[^'\\]*)*')|(^"[^"\\]*(?:\\.[^"\\]*)*")/;
Annotation: /@[a-zA-Z0-9_]+/;
WS: /\s+/;
CommentLine: /\/\/.*/;
NotComment: /((\*[^\/])|[^\s*\/]|\/[^\*])+/;
